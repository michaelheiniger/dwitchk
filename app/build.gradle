apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'de.mannodermaus.android-junit5'
apply plugin: 'kotlinx-serialization'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "ch.qscqlmpa.dwitch"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "ch.qscqlmpa.dwitch.base.CustomTestRunner"

        // Clears the app state between instrumented tests (activities, DB, ...)
        // Documentation: https://developer.android.com/training/testing/junit-runner
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    androidExtensions {
        experimental = true
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    android.sourceSets {
        test {
            java.srcDirs += "$projectDir/src/testShared"
        }

        androidTest {
            java.srcDirs += "$projectDir/src/testShared"
        }
    }
}

def dependencyGroup(Closure closure) {
    closure.delegate = dependencies
    return closure
}

// Dependencies
def daggerVersion = '2.25.3'
def jodaVersion = '2.10.1'
def kotlinxSerializationVersion = '1.0-M1-1.4.0-rc'
def lifecycleReactiveStreamsVersion = '2.2.0'
def recyclerViewVersion = '1.2.0-alpha01'
def roomVersion = '2.2.5'
def rxAndroidVersion = '2.1.1'
def rxJavaVersion = '2.2.17'
def rxRelayVersion = '2.1.1'
def timberVersion = '4.7.1'
def websocketVersion = '1.3.9'

def common = dependencyGroup {

    // Android
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0'
    implementation "androidx.core:core-ktx:+"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.1.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // Dagger
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    // Dagger Android
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    // Timber
    implementation "com.jakewharton.timber:timber:$timberVersion"

    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    implementation "com.jakewharton.rxrelay2:rxrelay:$rxRelayVersion"

    implementation "androidx.lifecycle:lifecycle-reactivestreams:$lifecycleReactiveStreamsVersion"

    // Joda time
    implementation "joda-time:joda-time:$jodaVersion"

    // Java-WebSocket
    implementation "org.java-websocket:Java-WebSocket:$websocketVersion"

    // Room database
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    // Serialization
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$kotlinxSerializationVersion"
}

def ui = dependencyGroup {
    implementation "com.google.android.material:material:1.0.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta5'
    implementation "androidx.recyclerview:recyclerview:$recyclerViewVersion"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.2'
}

def test = dependencyGroup {

    // JUnit4 (For ViewModel unit tests and instrumented tests)
    testImplementation 'junit:junit:4.12'

    // JUnit5 (for unit tests except those of ViewModel)
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.3.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.3.2"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.3.2"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:5.3.2"

    // MockK
    androidTestImplementation "io.mockk:mockk-android:1.9.3"
    testImplementation "io.mockk:mockk:1.9.3"

    testImplementation "org.assertj:assertj-core:3.11.1"
    androidTestImplementation "org.assertj:assertj-core:3.11.1"

    // Espresso
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:core:1.0.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestUtil 'androidx.test:orchestrator:1.1.0'

    // Dagger
    kaptAndroidTest "com.google.dagger:dagger-compiler:$daggerVersion"

    // To use androidx.arch.core.executor.testing.InstantTaskExecutorRule in ViewModel unit tests
    testImplementation 'android.arch.core:core-testing:1.1.1'
    testImplementation "androidx.room:room-testing:$roomVersion"
}

dependencies {
    implementation project(path: ':DwitchEngine')
    common()
    ui()
    test()
}
repositories {
    mavenCentral()
}
