plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlinx-serialization'

}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}


def dependencyGroup(Closure closure) {
    closure.delegate = dependencies
    return closure
}

def test = dependencyGroup {

    // JUnit4 (For ViewModel unit tests and instrumented tests)
    testImplementation 'junit:junit:4.12'

    // MockK
    testImplementation "io.mockk:mockk:1.9.3"
    androidTestImplementation "io.mockk:mockk-android:1.9.3"

    // AssertJ
    testImplementation "org.assertj:assertj-core:$assertjVersion"
    androidTestImplementation "org.assertj:assertj-core:$assertjVersion"

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:core:1.3.0'
    androidTestImplementation 'androidx.test:core-ktx:1.3.0'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.2'
    androidTestUtil 'androidx.test:orchestrator:1.3.1-alpha02'

    // Dagger
    kaptAndroidTest "com.google.dagger:dagger-compiler:$daggerVersion"

    testImplementation 'android.arch.core:core-testing:1.1.1'
    testImplementation "androidx.room:room-testing:$roomVersion"

    // Joda time
    implementation "joda-time:joda-time:$jodaVersion"
}


dependencies {
    implementation project(path: ':DwitchEngine')
    implementation project(path: ':DwitchModel')

    // Logging
    implementation "io.github.microutils:kotlin-logging-jvm:$kotlinLoggingVersion"
    implementation "org.slf4j:slf4j-android:$slf4jVersion"

    // Room database
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-rxjava3:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    // Dagger
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    // Serialization
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$kotlinxSerializationVersion"

    test()
}
